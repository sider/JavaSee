plugins {
    id 'application'
    id "com.intershop.gradle.javacc" version "4.0.0"
    id "io.freefair.lombok" version "5.2.1"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

version = '0.2.0'

def packagePrefix = 'com.github.sider.javasee'
mainClassName = "${packagePrefix}.Main"

jar {
    manifest {
        attributes('Implementation-Title': 'JavaSee',
                   'Implementation-Version': version)
    }
}

javacc {
    configs {
        template {
            inputFile = file('grammar/JavaSeeParser.jj')
            packageName = "${packagePrefix}.parser"
            lookahead = '2'
        }
    }
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.13.2'
    implementation 'com.github.javaparser:javaparser-core-serialization:3.13.2'
    implementation 'com.github.javaparser:javaparser-core:3.13.2'
    implementation 'org.yaml:snakeyaml:1.27'
    implementation 'args4j:args4j:2.33'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.fork = true
}

def distBaseName = 'JavaSee-bin'
shadowDistTar {
    archiveBaseName = distBaseName
}
shadowDistZip {
    archiveBaseName = distBaseName
}

distributions {
    shadow {
        contents {
            from 'CHANGELOG.md', 'CREDITS', 'LICENSE', 'README.md'
            from('doc') { into 'doc' }
            from('example') { into 'example' }
            from('logo') { into 'logo' }
        }
    }
}
