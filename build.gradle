plugins {
    id 'application'
    id "com.intershop.gradle.javacc" version "3.0.3"
    id "io.freefair.lombok" version "3.2.0"
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}
def packagePrefix = 'com.github.sider.javasee'
apply plugin: 'java'
apply plugin: 'com.intershop.gradle.javacc'
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}
shadowJar {
    baseName = 'JavaSee'
}
jar {
    baseName = 'JavaSee'
    manifest {
        attributes "Main-Class" : "${packagePrefix}.Main"
    }
}
javacc {
    configs {
        template {
            inputFile = file('grammar/JavaSeeParser.jj')
            packageName = "${packagePrefix}.parser"
            lookahead = '2'
        }
    }
}
test {
    useJUnitPlatform()
}
repositories {
    mavenCentral()
    jcenter()
}
dependencies {
    compile 'com.github.javaparser:javaparser-symbol-solver-core:3.13.2'
    compile 'com.github.javaparser:javaparser-core-serialization:3.13.2'
    compile 'com.github.javaparser:javaparser-core:3.13.2'
    compile 'org.yaml:snakeyaml:1.27'
    //compile "com.intershop.gradle.javacc:javacc-gradle-plugin:3.0.3"
    compile 'args4j:args4j:2.33'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}
application {
    mainClassName = "${packagePrefix}.Main"
}
run {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}
compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.fork = true
}
